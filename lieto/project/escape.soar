# enable RL
rl --set learning on
# use epsilon-greedy decision making
indifferent-selection --epsilon-greedy
# 10% deviation from greedy
indifferent-selection --epsilon 0.1



##################
### Initialize ###
##################
sp {propose*initialize*escape
    (state <s> ^superstate nil -^name)
-->
    (<s> ^operator <o> +)
    (<o> ^name initialize-escape)
}

sp {apply*initialize*escape
    (state <s> ^operator.name initialize-escape)
-->
    (<s> ^name escape ^previous <p>)
    (<p> ^name none)
    (write (crlf) |Inizia il gioco!|)
}



#############################
### Possible combinations ###
#############################

# twig + spring = slingshot
sp {propose*combine-twig-spring
    (state <s> ^name escape ^previous <previous-action>)
    (<previous-action> ^name <> combine-twig-spring)
-->
    (<s> ^operator <o> +)
    (<o> ^name combine-twig-spring)
}

sp {apply*combine-twig-spring
    (state <s> ^operator.name combine-twig-spring ^previous <p>)
-->
    (<s> ^previous <p> - <new-action> +)
    (<new-action> ^name combine-twig-spring ^reward 1)
    (write (crlf) |Hai combinato il rametto con la molla e creato una fionda!|)
}

# twig + rock = nothing
sp {propose*combine-rock-twig
    (state <s> ^name escape ^previous <previous-action>)
    (<previous-action> ^name <> combine-rock-twig)
-->
    (<s> ^operator <o> +)
    (<o> ^name combine-rock-twig)
}

sp {apply*combine-rock-twig
    (state <s> ^operator.name combine-rock-twig ^previous <p>)
-->
    (<s> ^previous <p> - <new-action> +)
    (<new-action> ^name combine-rock-twig ^reward -1)
    (write (crlf) |Hai combinato il rametto con la pietra senza ottenerne nulla.|)
}

# rock + spring = nothing
sp {propose*combine-rock-spring
    (state <s> ^name escape ^previous <previous-action>)
    (<previous-action> ^name <> combine-rock-spring)
-->
    (<s> ^operator <o> +)
    (<o> ^name combine-rock-spring)
}

sp {apply*combine-rock-spring
    (state <s> ^operator.name combine-rock-spring ^previous <p>)
-->
    (<s> ^previous <p> - <new-action> +)
    (<new-action> ^name combine-rock-spring ^reward -1)
    (write (crlf) |Hai combinato la pietra con la molla senza ottenerne nulla.|)
}



#############################################
### Reinforcement learning initialization ###
#############################################
sp {rl*combine-twig-spring*initialize
    (state <s> ^name escape ^operator <o> +)
    (<o> ^name combine-twig-spring)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*combine-rock-twig*initialize
    (state <s> ^name escape ^operator <o> +)
    (<o> ^name combine-rock-twig)
-->
    (<s> ^operator <o> = 0)
}

sp {rl*combine-rock-spring*initialize
    (state <s> ^name escape ^operator <o> +)
    (<o> ^name combine-rock-spring)
-->
    (<s> ^operator <o> = 0)
}



############################################
### Reinforcement learning apply rewards ###
############################################
sp {rl*apply-rewards
    (state <s> ^name escape ^reward-link <r> ^previous <previous-action>)
    (<previous-action> ^reward <reward>)
-->
    (<r> ^reward <rr>)
    (<rr> ^value <reward>)
    (write (crlf) |Reward: | <reward>)
}



##################
# Halt agent for testing
##################
sp {halt*escape
    (state <s> ^name escape ^previous <previous-action>)
    (<previous-action> ^name <> none)
-->
    (halt)
}